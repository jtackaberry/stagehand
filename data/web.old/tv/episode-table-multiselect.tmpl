<div id='episode-button-bar' style='position: absolute; margin-top: 80px; border: 1px solid #aaa; background-color: #eee; width: 45px; border-top-left-radius: 10px; border-bottom-left-radius:10px; padding: 15px 9px 15px 9px; -moz-box-shadow: inset 0 0 9px rgba(0, 0, 0, 0.10); -webkit-box-shadow: inset 0 0 14px rgba(0, 0, 0, 0.15); box-shadow: inset 0 0 14px rgba(0, 0, 0, 0.15); text-align: center; z-index: 0'>
    <div style='padding-bottom: 8px'>
        <button title='Schedule for download' id='button-episodes-status-need'>
            <img src='{{root}}/static/img/icon-20-status-need.png' style='width: 16px; padding: 3px 0' />
        </button>
    </div>
    <div style='padding-bottom: 8px'>
        <button title='Ignore episodes' id='button-episodes-status-ignore'>
            <img src='{{root}}/static/img/icon-20-status-ignore.png' style='width: 16px; padding: 3px 0' />
        </button>
    </div>
    <div>
        <button title='Delete downloaded files then ignore' id='button-episodes-status-delete'>
            <img src='{{root}}/static/img/icon-16-delete.png' style='padding: 3px 0' />
        </button>
    </div>
</div>
<script type='text/coffeescript'>
    open_button_bar_if_needed = ->
        if $('#{{tableid}} input[type=checkbox]:checked').size()
            $('#episode-button-bar').animate marginLeft: -64, 200
        else
            $('#episode-button-bar').animate marginLeft: 0, 200
            $('#episode-button-bar button').poshytip 'hide'

    $ ->
        $('#{{tableid}} input[type=checkbox]').click (event) ->
            event.stopPropagation()
            defer 0, open_button_bar_if_needed

        $('#{{tableid}} tr:first input[type=checkbox]').click (event) ->
            $('#{{tableid}} input[type=checkbox]').prop('checked', @.checked)

        $('tr.heading input[type=checkbox]').click (event) ->
            group = $(@).parents('tr').attr('id')
            $("tr.#{group} input[type=checkbox]").prop('checked', @.checked)

        $('#{{tableid}} input[type=checkbox]').prop('checked', false)

    # Must use onload rather than DOM ready because height of not-yet-loaded
    # images would otherwise not be included in top offset of button bar.
    $(@).bind 'load', ->
        bar = $('#episode-button-bar')
        bar_top = bar.offset().top - 40
        $(@).unbind 'scroll.stagehand'
        $(@).bind 'scroll.stagehand', ->
            if $(@).scrollTop() > bar_top
                bar.css position: 'fixed', top: 0, marginTop: '40px'
            else
                bar.css position: 'absolute', top: 'inherit', marginTop: '80px'
        $(@).trigger 'scroll.stagehand'
        open_button_bar_if_needed()
        #$('#episode-button-bar').css(display: 'none', visibility: 'visible')

    $('#episode-button-bar button').poshytip
        alignY: 'center'
        alignX: 'right'
        alignTo: 'target'
        offsetX: 5
        showTimeout: 100

    $('#episode-button-bar button').click ->
        eps_by_show = {}
        status = @id.replace('button-episodes-status-', '')
        for item in $('#{{tableid}} tr.ep input[type=checkbox]:checked').parents('tr')
            [showid, epcode] = item.id.split('-')
            if not eps_by_show[showid]
                eps_by_show[showid] = []
            eps_by_show[showid].push(epcode)

        for showid, eplist of eps_by_show
            do (showid, eplist) ->
                stagehand.api("/api/shows/#{showid}/episodes/#{eplist.join(',')}/status?value=#{status}", null, 'POST')
                    .done ({statuses}) =>
                        for code, [status, title] of statuses
                            # showid contains a colon, so need to use this form to prevent jquery from
                            # interpreting it as a selector.
                            $("[id='#{showid}-#{code}'] img")
                                .prop('src', "{{root}}/static/img/icon-20-status-#{status}.png")
                                .prop('title', title)
                                .prop('alt', title)
                        $('#{{tableid}} input[type=checkbox]').prop('checked', false)
                        open_button_bar_if_needed()

                    .fail ({message, xhr}) ->
                        if xhr.status == 404
                            # This should never happen.  (No, really.)
                            alert "Failed: one of the episodes selected doesn't exist or unknown status code"
                        else
                            alert "Failed (multiselect) status=#{xhr.status}: #{message}"

</script>
